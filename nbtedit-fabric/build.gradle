plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'me.shedaniel.unified-publishing' version '0.1.+'
}

loom {
    runs {
        client {
            client()
            configName = "Fabric Client"
            ideConfigGenerated = true
            runDir = 'run/client'
        }

        server {
            server()
            configName = "Fabric Server"
            ideConfigGenerated = true
            runDir = 'run/server'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${rootProject.parchment_minecraft_version}:${rootProject.parchment_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"

    modRuntimeOnly ("com.terraformersmc:modmenu:${rootProject.mod_menu_version}") {
        exclude group: 'net.fabricmc'
        exclude group: 'net.fabricmc.fabric-api'
    }

    api project(':nbtedit-common')
}

jar.finalizedBy(shadowJar)

shadowJar {
    dependencies {
        include project(':nbtedit-common')
    }
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = project.name
            version = mavenVersion
            from components.java
        }
    }
}

unifiedPublishing {
    project {
        displayName = "NBTEdit Fabric ${project.minecraft_version}-${project.mod_version}"
        version = publishVersion
        gameVersions = [project.minecraft_version]
        gameLoaders = ['fabric']
        releaseType = 'beta'
        changelog = 'See the GitHub repository for more information. \nhttps://github.com/qyl27/NBTEdit'

        mainPublication tasks.remapJar

        relations {
            depends {
                curseforge = 'fabric-api'
                modrinth = 'fabric-api'
            }
        }

        curseforge {
            token = System.getenv('CURSEFORGE_TOKEN')
            id = '678133'
        }

        modrinth {
            token = System.getenv('MODRINTH_TOKEN')
            id = 'Vr2eDeCw'
        }
    }
}
