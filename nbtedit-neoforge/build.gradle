plugins {
    id 'java'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.120'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'me.shedaniel.unified-publishing' version '0.1.+'
}

runs {
    configureEach {
//      systemProperty 'forge.logging.markers', 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'info'
        systemProperty 'fml.earlyprogresswindow', 'false'

        modSource sourceSets.main
        modSource project(':nbtedit-common').sourceSets.main
    }

    client {
        workingDirectory file('run/client')
        systemProperty 'forge.enabledGameTestNamespaces', 'nbtedit'
    }

    server {
        workingDirectory file('run/server')
        systemProperty 'forge.enabledGameTestNamespaces', 'nbtedit'
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', 'nbtedit'
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${rootProject.neoforge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation project(':nbtedit-common')
    shadow project(':nbtedit-common')
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

jar.finalizedBy(shadowJar)

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = project.name
            version = mavenVersion
            from components.java
        }
    }
}

unifiedPublishing {
    project {
        displayName = "NBTEdit NeoForge ${project.minecraft_version}-${project.mod_version}"
        version = publishVersion
        gameVersions = [project.minecraft_version]
        gameLoaders = ['neoforge']
        releaseType = 'release'
        changelog = 'See the GitHub repository for more information. \nhttps://github.com/qyl27/NBTEdit'

        mainPublication tasks.shadowJar

        curseforge {
            token = System.getenv('CURSEFORGE_TOKEN')
            id = '678133'
        }

        modrinth {
            token = System.getenv('MODRINTH_TOKEN')
            id = 'Vr2eDeCw'
        }
    }
}
