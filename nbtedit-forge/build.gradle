plugins {
    id 'com.gradleup.shadow'
    id 'me.shedaniel.unified-publishing'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(':nbtedit-common').loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    common(project(path: ':nbtedit-common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':nbtedit-common', configuration: 'transformProductionForge')
}

shadowJar {
    exclude 'fabric.mod.json'
    exclude 'architectury.common.json'

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(':nbtedit-common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = project.name
            version = mavenVersion
            from components.java
        }
    }
}

unifiedPublishing {
    project {
        displayName = "NBTEdit Forge ${project.minecraft_version}-${project.mod_version}"
        version = publishVersion
        gameVersions = [project.minecraft_version]
        gameLoaders = ['forge']
        releaseType = 'release'
        changelog = 'See the GitHub repository for more information. \nhttps://github.com/qyl27/NBTEdit'

        mainPublication tasks.remapJar

        curseforge {
            token = Objects.requireNonNullElse(System.getenv('CURSEFORGE_TOKEN'), "")
            id = '678133'
        }

        modrinth {
            token = Objects.requireNonNullElse(System.getenv('MODRINTH_TOKEN'), "")
            id = 'Vr2eDeCw'
        }
    }
}
