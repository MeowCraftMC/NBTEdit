plugins {
    id 'java'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'me.shedaniel.unified-publishing' version '0.1.+'
}

minecraft {
    mappings channel: 'parchment', version: "${rootProject.parchment_version}-${rootProject.parchment_minecraft_version}"

    runs {
        configureEach {
//            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            property 'fml.earlyprogresswindow', 'false'
            ideaModule "${rootProject.name}.${project.name}.main"

            mods {
                nbtedit {
                    source sourceSets.main
                    source project(':nbtedit-common').sourceSets.main
                }
            }
        }

        client {
            taskName = 'ForgeClient'
            workingDirectory file('run/client')
            property 'forge.enabledGameTestNamespaces', 'nbtedit'
        }

        server {
            taskName = 'ForgeServer'
            workingDirectory file('run/server')
            property 'forge.enabledGameTestNamespaces', 'nbtedit'
            args '--nogui'
        }

        gameTestServer {
            taskName = 'ForgeGameTest'
            property 'forge.enabledGameTestNamespaces', 'nbtedit'
        }

        data {
            taskName = 'ForgeData'
            workingDirectory file('run/data')

            args '--mod', 'nbtedit',
                    '--all',
                    '--output', file('../nbtedit-common/src/generated/resources'),
                    '--existing', file('../nbtedit-common/src/main/resources')
        }
    }
}

repositories {
    maven {
        name = 'MinecraftForge'
        url = 'https://files.minecraftforge.net/maven/'
    }
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    minecraftLibrary project(':nbtedit-common')
    shade project(':nbtedit-common')
}

jar {
    finalizedBy(shadowJar)
    getFinalizedBy().println()
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

reobf {
    shadowJar {}
}

shadowJar {
    configurations = [project.configurations.shade]
//    dependencies {
//        include project(':nbtedit-common')
//    }
    finalizedBy 'reobfShadowJar'
}

//tasks.register('reobfJar', net.minecraftforge.gradle.userdev.task.RenameJarInPlace) {
//    input.set shadowJar.archiveFile
//    dependsOn shadowJar
//}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = project.name
            version = mavenVersion
            from components.java
        }
    }
}

unifiedPublishing {
    project {
        displayName = "NBTEdit Forge ${project.minecraft_version}-${project.mod_version}"
        version = publishVersion
        gameVersions = [project.minecraft_version]
        gameLoaders = ['forge']
        releaseType = 'release'
        changelog = 'See the GitHub repository for more information. \nhttps://github.com/qyl27/NBTEdit'

        mainPublication shadowJar

        curseforge {
            token = System.getenv('CURSEFORGE_TOKEN')
            id = '678133'
        }

        modrinth {
            token = System.getenv('MODRINTH_TOKEN')
            id = 'Vr2eDeCw'
        }
    }
}
