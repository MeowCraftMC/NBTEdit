import java.time.OffsetDateTime

plugins {
    id "architectury-plugin" version "${architect_plugin_version}"
    id "dev.architectury.loom" version "${architectury_loom_version}" apply false
}

def ENV = System.getenv()

var publishVersion = project.mod_version
publishVersion += "+mc${project.minecraft_version}"
var mavenVersion = publishVersion
if (!ENV.MOD_RELEASE) {
    mavenVersion += '-SNAPSHOT'
}

ext.publishVersion = publishVersion
ext.mavenVersion = mavenVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'architectury-plugin'
    apply plugin: 'dev.architectury.loom'

    architectury {
        minecraft = project.minecraft_version
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_version}@zip")
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.release = 21
    }

    java {
        withSourcesJar()
    }

    processResources {
        var resourcesToReplace = ['build_info.properties', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', 'pack.mcmeta', 'fabric.mod.json']
        var replaceTokens = [
                'archives_base_name'              : rootProject.archives_base_name,
                'mod_id'                          : rootProject.mod_id,
                'mod_name'                        : rootProject.mod_name,
                'mod_full_name'                   : rootProject.mod_full_name,
                'mod_version'                     : rootProject.mod_version,
                'minecraft_version'               : rootProject.minecraft_version,
                'parchment_minecraft_version'     : rootProject.parchment_minecraft_version,
                'parchment_version'               : rootProject.parchment_version,
                'fabric_loader_version'           : rootProject.fabric_loader_version,
                'fabric_api_version'              : rootProject.fabric_api_version,
                'mod_menu_version'                : rootProject.mod_menu_version,
                'forge_version'                   : rootProject.forge_version,
                'neoforge_version'                : rootProject.neoforge_version,
                'forge_loader_version_range'      : rootProject.forge_loader_version_range,
                'forge_version_range'             : rootProject.forge_version_range,
                'forge_minecraft_version_range'   : rootProject.forge_minecraft_version_range,
                'neoforge_loader_version_range'   : rootProject.neoforge_loader_version_range,
                'neoforge_version_range'          : rootProject.neoforge_version_range,
                'neoforge_minecraft_version_range': rootProject.neoforge_minecraft_version_range,
                'fabric_loader_version_range'     : rootProject.fabric_loader_version_range,
                'fabric_api_version_range'        : rootProject.fabric_api_version_range,
                'fabric_permission_api_version_range'        : rootProject.fabric_permission_api_version_range,
                'fabric_minecraft_version_range'  : rootProject.fabric_minecraft_version_range,
                'build_time'                      : OffsetDateTime.now(),
                'build_version'                   : publishVersion
        ]

        inputs.properties replaceTokens
        filesMatching(resourcesToReplace) {
            expand replaceTokens
        }

        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

allprojects {
    apply plugin: 'maven-publish'

    group = 'cx.rain.mc.nbtedit'
    version = project.mod_version

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }

        maven {
            name = 'Mod Menu'
            url = 'https://maven.terraformersmc.com/releases'
        }

        maven {
            name = 'Curse maven'
            url = 'https://cursemaven.com'
            content {
                includeGroup 'curse.maven'
            }
        }

        maven {
            name = 'NeoForged'
            url = 'https://maven.neoforged.net/releases/'
        }

        mavenCentral()
    }

    publishing {
        repositories {
            mavenLocal()

            maven {
                name = 'YuluoMaven'

                def releaseUrl = 'https://maven.yuluo.dev/repository/maven-releases/'
                def snapshotUrl = 'https://maven.yuluo.dev/repository/maven-snapshots/'
                url = ENV.MOD_RELEASE ? releaseUrl : snapshotUrl

                credentials {
                    username ENV.MOD_MAVEN_USER
                    password ENV.MOD_MAVEN_PASS
                }
            }
        }
    }
}
